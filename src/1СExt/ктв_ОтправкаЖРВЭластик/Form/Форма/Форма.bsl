#Область ОбработчикиЭлементовФормы

// Процедура - обработка начала выбора файла
//
&НаКлиенте
Процедура ПутьККаталогуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Заголовок = "Каталог журнала регистрации 1С";

	ЗавершениеВыбораФайла = Новый ОписаниеОповещения("ПутьКФайлуНачалоВыбораЗавершение", ЭтаФорма);
	
	Диалог.Показать(ЗавершениеВыбораФайла);
	
КонецПроцедуры // ПутьКФайлуНачалоВыбора()

// Процедура - продолжение обработки выбора файла
//
&НаКлиенте
Процедура ПутьКФайлуНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если НЕ ТипЗнч(ВыбранныеФайлы) = Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПутьККаталогу = ВыбранныеФайлы[0];
	
КонецПроцедуры // ПутьКФайлуНачалоВыбораЗавершение()

&НаКлиенте
Процедура РазмерПакетаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	РазмерПакета = РазмерПакета + Направление * 100;
	
КонецПроцедуры // РазмерПакетаРегулирование()

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - обработчик команды "Прочитать"
//
&НаСервере
Функция ПрочитатьНаСервере(ПутьКФайлу)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ОбработкаОбъект.НастройкиСоединения = Новый Структура();
	ОбработкаОбъект.НастройкиСоединения.Вставить("Сервис_Сервер", АдресЭластик);
	ОбработкаОбъект.НастройкиСоединения.Вставить("Сервис_АдресРесурса", "");
	ОбработкаОбъект.НастройкиСоединения.Вставить("Сервис_Пользователь", "");
	ОбработкаОбъект.НастройкиСоединения.Вставить("Сервис_Пароль", "");
	ОбработкаОбъект.НастройкиСоединения.Вставить("УстановленноеСоединение", Неопределено);
	ОбработкаОбъект.НастройкиСоединения.Вставить("УстановленноеСоединение_Использование", Истина);

	ОбработкаОбъект.ЗаголовокИндекса = НРег(Клиент + "_" + ИмяБазы);
	ОбработкаОбъект.РазмерПакетаДляОтправки = РазмерПакета;

	Файлы = НайтиФайлы(ПутьККаталогу, "*.lgp", Ложь);
	
	Для Каждого ТекФайл Из Файлы Цикл
		ОбработкаОбъект.Прочитать(ТекФайл.ПолноеИмя);
		ОбработкаОбъект.ПолучитьРезультат(Истина);
	КонецЦикла;
	
	Возврат ОбработкаОбъект.ПолучитьРезультат(Истина);
	
КонецФункции // ПрочитатьНаСервере()

// Процедура - обработчик команды "Прочитать"
//
&НаКлиенте
Процедура Прочитать(Команда)
	
	Результат = ПрочитатьНаСервере(ПутьККаталогу);
	
КонецПроцедуры // Прочитать()

#КонецОбласти

